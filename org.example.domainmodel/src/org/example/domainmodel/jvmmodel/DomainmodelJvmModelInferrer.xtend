/*
 * generated by Xtext unknown
 */
package org.example.domainmodel.jvmmodel

import com.google.inject.Inject
import org.eclipse.xtext.naming.IQualifiedNameProvider
import org.eclipse.xtext.xbase.jvmmodel.AbstractModelInferrer
import org.eclipse.xtext.xbase.jvmmodel.IJvmDeclaredTypeAcceptor
import org.eclipse.xtext.xbase.jvmmodel.JvmTypesBuilder
import org.example.domainmodel.domainmodel.Entity
import org.example.domainmodel.domainmodel.Operation
import org.example.domainmodel.domainmodel.Property

/**
 * <p>Infers a JVM model from the source model.</p> 
 * 
 * <p>The JVM model should contain all elements that would appear in the Java code 
 * which is generated from the source model. Other models link against the JVM model rather than the source model.</p>     
 */
class DomainmodelJvmModelInferrer extends AbstractModelInferrer {
	@Inject extension JvmTypesBuilder

	@Inject extension IQualifiedNameProvider

	def dispatch void infer(Entity element, IJvmDeclaredTypeAcceptor acceptor, boolean isPrelinkingPhase) {
		acceptor.accept(element.toClass(element.fullyQualifiedName)) [
			documentation = element.documentation
			if (element.superType != null)
				superTypes += element.superType.cloneWithProxies
			for (feature : element.features) {
				switch feature {
					Property: {
						members += feature.toField(feature.name, feature.type)
						members += feature.toGetter(feature.name, feature.type)
						members += feature.toSetter(feature.name, feature.type)
					}
					Operation: {
						members += feature.toMethod(feature.name, feature.type) [
							documentation = feature.documentation
							for (p : feature.params) {
								parameters += p.toParameter(p.name, p.parameterType)
							}
							body = feature.body
						]
					}
				}
			}
		]
	}
}
